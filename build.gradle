/////////////
// PLUGINS //
/////////////

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
    id "com.jfrog.bintray" version "1.8.5"
    id "java-library"
    id "maven-publish"
    id "jacoco"
    id "org.jlleitschuh.gradle.ktlint" version "9.4.1"
    id "io.gitlab.arturbosch.detekt" version "1.14.2"
    id "com.github.ben-manes.versions" version "0.36.0"
}

//////////////////
// DEPENDENCIES //
//////////////////

repositories {
    jcenter()
}

ext {
    slf4jVersion = "1.7.30"
}

dependencies {
    // KOTLIN
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // SLF4J
    api "koriit.kotlin:slf4j-utils:0.7.1"
    implementation "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    implementation "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    implementation "org.slf4j:jul-to-slf4j:$slf4jVersion"

    // LOGBACK
    api "ch.qos.logback:logback-classic:1.2.3"
    api "net.logstash.logback:logstash-logback-encoder:6.4"

    // TESTING
    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
}

/////////////
// UPDATES //
/////////////

def isNonStable = { String version ->
    def regex = /^[0-9,.v-]+((-r)|(release)|(final)|(ga))?$/
    return !(version.toLowerCase() ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

/////////////
// COMPILE //
/////////////

targetCompatibility = 1.8
sourceCompatibility = targetCompatibility

def compilerArgs = [
        "-Xjsr305=strict"
]

compileKotlin {
    dependsOn ktlintFormat
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs += compilerArgs
        jvmTarget = "$targetCompatibility"
    }
}

//////////
// TEST //
//////////

ktlint {
    version = "0.39.0"
    verbose = true
}

detekt {
    config = files("detekt.yml")
    buildUponDefaultConfig = true
}

test {
    useJUnitPlatform {
        excludeTags hasProperty("fast") ? "Slow" : "None"
    }
}

jacoco {
    toolVersion = "0.8.4"
}

if (!hasProperty("fast")) test.finalizedBy(jacocoTestReport)

/////////////
// PUBLISH //
/////////////

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

publishing {
    publications {
        bintrayMaven(MavenPublication) {
            groupId = project.group
            artifactId = projectName
            version = project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "SLF4J+Logback Utils"
                description = "SLF4J+Logback utils for Kotlin"
                url = "https://github.com/Koriit/slf4j-utils-logback"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/Koriit/slf4j-utils-logback/blob/master/LICENSE"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getProperty("bintray.user")
    key = System.getProperty("bintray.key")
    publications = ["bintrayMaven"]

    pkg {
        repo = "kotlin"
        name = "slf4j-utils-logback"
        version {
            name = project.version
            released = new Date()
        }
    }
}

///////////
// OTHER //
///////////

task getVersion {
    doLast {
        print project.version
    }
}

task fmt {
    dependsOn ktlintFormat
}
